buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.yaml:snakeyaml:1.16'
    }
}

plugins {
    id "de.undercouch.download" version "3.0.0"
}

import org.yaml.snakeyaml.*
import de.undercouch.gradle.tasks.download.Download

ext {
    yamlFile = file('chapters.yaml')
}

task listUtterances {
    def segmentationDir = file('segmentationdata')
    inputs.files segmentationDir
    outputs.files yamlFile
    doLast {
        def utterances = []
        segmentationDir.eachFile { trsFile ->
            def parser = new XmlSlurper()
            // from http://stackoverflow.com/a/30754000
            parser.setFeature("http://apache.org/xml/features/disallow-doctype-decl", false)
            parser.setFeature("http://apache.org/xml/features/nonvalidating/load-external-dtd", false)
            def xml = parser.parse(trsFile)
            def chapter = [
                    filename  : xml.'**'.find { it.name() == 'Trans' }.@audio_filename as String,
                    utterances: []
            ]
            def start = 0
            xml.'**'.findAll { it.name() == 'Sync' }.tail().each { sync ->
                def end = Float.parseFloat(sync.@time as String)
                chapter.utterances << [
                        start: start,
                        end  : end
                ]
                start = end
            }
            utterances << chapter
        }
        def options = new DumperOptions()
        options.defaultFlowStyle = DumperOptions.FlowStyle.BLOCK
        yamlFile.text = new Yaml(options).dump(utterances)
    }
}

task download(type: Download) {
    inputs.files yamlFile
    src(new Yaml().load(yamlFile.text).collect { chapter ->
        "http://www.archive.org/download/nagelaten_bekentenis_0806_librivox/${chapter.filename}.mp3"
    })
    dest "$buildDir/download"
    overwrite false
}

task splitAudio {
    def wavDir = file("$buildDir/wav")
    inputs.files yamlFile, download
    outputs.dir wavDir
    doFirst {
        wavDir.mkdirs()
    }
    doLast {
        new Yaml().load(yamlFile.text).eachWithIndex { chapter, c ->
            def mp3File = file("$buildDir/${chapter.filename}.mp3")
            chapter.utterances.eachWithIndex { utterance, u ->
                def wavFile = file("$wavDir/${chapter.filename}_${(c + 1 as String).padLeft(2, '0')}_${(u + 1 as String).padLeft(3, '0')}.wav")
                exec {
                    commandLine 'sox', mp3File, wavFile, 'trim', utterance.start, "=$utterance.end", 'rate', 16000
                }
            }
        }
    }
}
